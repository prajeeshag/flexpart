  !*****************************************************************************
  !                                                                            *
  ! 2021 L. Bakels: This module contains all turbulence related subroutines    *
  ! 2023 PS: include psih, psim into this module
  !                                                                            *
  !*****************************************************************************

module turbulence_mod
  use par_mod
  use com_mod
  use particle_mod

  implicit none

  real :: ust,wst,ol,h,zeta,sigu,sigv,tlu,tlv,tlw
  real :: sigw,dsigwdz,dsigw2dz

!$OMP THREADPRIVATE(ust,wst,ol,h,zeta,sigu,sigv,tlu,tlv,tlw, &
!$OMP sigw,dsigwdz,dsigw2dz)

contains

subroutine turbulence_boundarylayer(ipart,nrand,dt,zts,rhoa,rhograd,thread)
  
  use cbl_mod
  
  implicit none 

  integer, intent(in) ::   &
    ipart,                 & ! particle index
    thread                   ! number of the omp thread
  integer, intent(inout) ::&
    nrand                    ! random number used for turbulence
  real,intent(in) ::       &
    dt,                    & ! real(ldt)
    rhoa,                  & ! air density, used in CBL
    rhograd                  ! vertical gradient of the air density, used in CBL
  real,intent(inout) ::    &
    zts                      ! local 'real' copy of the particle position
  real ::                     &
    delz,                     & ! change in vertical position due to turbulence
    ru,rv,rw,wp,icbt_r,       & ! used for computing turbulence
    dtf,rhoaux,dtftlw,ath,bth,& ! CBL related
    ptot_lhh,Q_lhh,phi_lhh,   & ! CBL related
    old_wp_buf,dcas,dcas1,    & ! CBL related
    del_test                    ! CBL related
  integer ::                  &
    flagrein,                 & ! flag used in CBL scheme
    icbt,                     &
    i                           ! loop variable

  ! tlw,dsigwdz and dsigw2dz are defined in hanna
    if (turbswitch) then
      call hanna(zts)
    else
      call hanna1(zts)
    endif

  !*****************************************
  ! Determine the new diffusivity velocities
  !*****************************************

  ! Horizontal components
  !**********************
    if (nrand+1.gt.maxrand) nrand=1
    if (dt/tlu.lt..5) then
      part(ipart)%turbvel%u=(1.-dt/tlu)*part(ipart)%turbvel%u+rannumb(nrand)*sigu*sqrt(2.*dt/tlu)
    else
      ru=exp(-dt/tlu)
      part(ipart)%turbvel%u=ru*part(ipart)%turbvel%u+rannumb(nrand)*sigu*sqrt(1.-ru**2)
    endif
    if (dt/tlv.lt..5) then
      part(ipart)%turbvel%v=(1.-dt/tlv)*part(ipart)%turbvel%v+rannumb(nrand+1)*sigv*sqrt(2.*dt/tlv)
    else
      rv=exp(-dt/tlv)
      part(ipart)%turbvel%v=rv*part(ipart)%turbvel%v+rannumb(nrand+1)*sigv*sqrt(1.-rv**2)
    endif
    nrand=nrand+2


    if (nrand+ifine.gt.maxrand) nrand=1
    rhoaux=rhograd/rhoa
    dtf=dt*fine

    dtftlw=dtf/tlw

  ! Loop over ifine short time steps for vertical component
  !********************************************************
    wp=part(ipart)%turbvel%w
    icbt=part(ipart)%icbt
    do i=1,ifine
      icbt_r=real(icbt)
  ! Determine the drift velocity and density correction velocity
  !*************************************************************
      
      if (turbswitch) then
        if (dtftlw.lt..5) then
  !*************************************************************
  !************** CBL options added by mc see routine cblf90 ***
          ! LB needs to be checked if this works with openmp
          if (cblflag.eq.1) then  !modified by mc
            if (-h/ol.gt.5) then  !modified by mc
              flagrein=0
              nrand=nrand+1
              old_wp_buf=wp
              call cbl(wp,zts,ust,wst,h,rhoa,rhograd,&
                sigw,dsigwdz,tlw,ptot_lhh,Q_lhh,phi_lhh,ath,bth,ol,flagrein) !inside the routine for inverse time
              wp=(wp+ath*dtf+&
                bth*rannumb(nrand)*sqrt(dtf))*icbt_r
              delz=wp*dtf
              if ((flagrein.eq.1).or.(wp.ne.wp).or.((wp-1.).eq.wp)) then
                call re_initialize_particle(zts,ust,wst,h,sigw,old_wp_buf,nrand,ol)
                wp=old_wp_buf
                delz=wp*dtf
                nan_count(thread+1)=nan_count(thread+1)+1
              end if             
            else 
              nrand=nrand+1
              old_wp_buf=wp
              ath=-wp/tlw+sigw*dsigwdz+&
                wp*wp/sigw*dsigwdz+sigw*sigw/rhoa*rhograd  !1-note for inverse time should be -wp/tlw*ldirect+... calculated for wp=-wp
                                                                                  !2-but since ldirect =-1 for inverse time and this must be calculated for (-wp) and
                                                                                  !3-the gaussian pdf is symmetric (i.e. pdf(w)=pdf(-w) ldirect can be discarded
              bth=sigw*rannumb(nrand)*sqrt(2.*dtftlw)
              wp=(wp+ath*dtf+bth)*icbt_r  
              delz=wp*dtf
              if ((wp.ne.wp).or.((wp-1.).eq.wp)) then ! Catch infinity or NaN
                nrand=nrand+1                      
                wp=sigw*rannumb(nrand)
                delz=wp*dtf
                nan_count(thread+1)=nan_count(thread+1)+1
              end if
            end if
  !******************** END CBL option *******************************            
  !*******************************************************************            
          else
               wp=((1.-dtftlw)*wp+rannumb(nrand+i)*sqrt(2.*dtftlw) &
               +dtf*(dsigwdz+rhoaux*sigw))*icbt_r
               delz=wp*sigw*dtf
          end if
        else
          rw=exp(-dtftlw)
          wp=(rw*wp+rannumb(nrand+i)*sqrt(1.-rw**2) &
               +tlw*(1.-rw)*(dsigwdz+rhoaux*sigw))*icbt_r
          delz=wp*sigw*dtf
        endif
        
      else
        rw=exp(-dtftlw)
        wp=(rw*wp+rannumb(nrand+i)*sqrt(1.-rw**2)*sigw &
             +tlw*(1.-rw)*(dsigw2dz+rhoaux*sigw**2))*icbt_r
        delz=wp*dtf
      endif

  !****************************************************
  ! Compute turbulent vertical displacement of particle
  !****************************************************

      if (abs(delz).gt.h) delz=mod(delz,h)

  ! Determine if particle transfers to a "forbidden state" below the ground
  ! or above the mixing height
  !************************************************************************

      if (delz.lt.-zts) then         ! reflection at ground
        icbt=-1
        call set_z(ipart,-zts-delz)
      else if (delz.gt.(h-zts)) then ! reflection at h
        icbt=-1
        call set_z(ipart,-zts-delz+2.*h)
      else                         ! no reflection
        icbt=1
        call set_z(ipart,zts+delz)
      endif

      if (i.ne.ifine) then
        zeta=zts/h
        call hanna_short(zts)
      endif
      zts=real(part(ipart)%z)
    end do
    part(ipart)%turbvel%w=wp
    part(ipart)%icbt=icbt
    if (cblflag.ne.1) nrand=nrand+i
end subroutine turbulence_boundarylayer

subroutine turbulence_stratosphere(dt,nrand,ux,vy,wp,tropop,zts)

  implicit none
  
  integer, intent(inout) ::       &
    nrand                           ! random number used for turbulence
  real, intent(inout) ::          &
    ux,vy,wp                        ! random turbulent velocities above PBL
  real, intent(in) ::             &
    tropop,                       & ! height of troposphere
    zts,                          & ! height of particle
    dt                              ! real(ldt)
  real ::                         &
    uxscale,wpscale,              & ! factor used in calculating turbulent perturbations above PBL
    weight                          ! transition above the tropopause

  if (zts.lt.tropop) then  ! in the troposphere
    uxscale=sqrt(2.*d_trop/dt)
    if (nrand+1.gt.maxrand) nrand=1
    ux=rannumb(nrand)*uxscale
    vy=rannumb(nrand+1)*uxscale
    nrand=nrand+2
    wp=0.
  else if (zts.lt.tropop+1000.) then     ! just above the tropopause: make transition
    weight=(zts-tropop)/1000.
    uxscale=sqrt(2.*d_trop/dt*(1.-weight))
    if (nrand+2.gt.maxrand) nrand=1
    ux=rannumb(nrand)*uxscale
    vy=rannumb(nrand+1)*uxscale
    wpscale=sqrt(2.*d_strat/dt*weight)
    wp=rannumb(nrand+2)*wpscale+d_strat/1000.
    nrand=nrand+3
  else                 ! in the stratosphere
    if (nrand.gt.maxrand) nrand=1
    ux=0.
    vy=0.
    wpscale=sqrt(2.*d_strat/dt)
    wp=rannumb(nrand)*wpscale
    nrand=nrand+1
  endif
end subroutine turbulence_stratosphere

subroutine turbulence_mesoscale(nrand,dxsave,dysave,ipart,usig,vsig,wsig,wsigeta,eps_eta)
  
  implicit none

  integer, intent(inout) ::       &
    nrand                           ! random number used for turbulence
  integer, intent(in) ::          &
    ipart                              ! particle index
  real, intent(in) ::             &
    eps_eta,usig,vsig,wsig,wsigeta
  real, intent(inout) ::          &
    dxsave,dysave                   ! accumulated displacement in long and lat
  real ::                         &
    r,rs,                         & ! mesoscale related
    ux,vy                           ! random turbulent velocities above PBL

  r=exp(-2.*real(abs(lsynctime))/real(lwindinterv))
  rs=sqrt(1.-r**2)
  if (nrand+2.gt.maxrand) nrand=1
  part(ipart)%mesovel%u=r*part(ipart)%mesovel%u+rs*rannumb(nrand)*usig*turbmesoscale
  part(ipart)%mesovel%v=r*part(ipart)%mesovel%v+rs*rannumb(nrand+1)*vsig*turbmesoscale
  dxsave=dxsave+part(ipart)%mesovel%u*real(lsynctime)
  dysave=dysave+part(ipart)%mesovel%v*real(lsynctime)

  select case (wind_coord_type)
    case ('ETA')
      part(ipart)%mesovel%w=r*part(ipart)%mesovel%w+rs*rannumb(nrand+2)*wsigeta*turbmesoscale
      call update_zeta(ipart,part(ipart)%mesovel%w*real(lsynctime))
      if (part(ipart)%zeta.ge.1.) call set_zeta(ipart,1.-(part(ipart)%zeta-1.))
      if (part(ipart)%zeta.eq.1.) call update_zeta(ipart,-eps_eta)

    case ('METER')
      part(ipart)%mesovel%w=r*part(ipart)%mesovel%w+rs*rannumb(nrand+2)*wsig*turbmesoscale
      call update_z(ipart,part(ipart)%mesovel%w*real(lsynctime))
      if (part(ipart)%z.lt.0.) call set_z(ipart,-1.*part(ipart)%z)    ! if particle below ground -> refletion

    case default
      part(ipart)%mesovel%w=r*part(ipart)%mesovel%w+rs*rannumb(nrand+2)*wsig*turbmesoscale
      call update_z(ipart,part(ipart)%mesovel%w*real(lsynctime))
      if (part(ipart)%z.lt.0.) call set_z(ipart,-1.*part(ipart)%z)    ! if particle below ground -> refletion
  end select
end subroutine turbulence_mesoscale

subroutine hanna(z)
  !                 i
  !*****************************************************************************
  !                                                                            *
  !   Computation of \sigma_i and \tau_L based on the scheme of Hanna (1982)   *
  !                                                                            *
  !   Author: A. Stohl                                                         *
  !                                                                            *
  !   4 December 1997                                                          *
  !                                                                            *
  !*****************************************************************************
  !                                                                            *
  ! Variables:                                                                 *
  ! dsigwdz [1/s]     vertical gradient of sigw                                *
  ! ol [m]            Obukhov length                                           *
  ! sigu, sigv, sigw  standard deviations of turbulent velocity fluctuations   *
  ! tlu [s]           Lagrangian time scale for the along wind component.      *
  ! tlv [s]           Lagrangian time scale for the cross wind component.      *
  ! tlw [s]           Lagrangian time scale for the vertical wind component.   *
  ! ust, ustar [m/s]  friction velocity                                        *
  ! wst, wstar [m/s]  convective velocity scale                                *
  !                                                                            *
  !*****************************************************************************

  implicit none

  real :: corr,z


  !**********************
  ! 1. Neutral conditions
  !**********************

  if (h/abs(ol).lt.1.) then
    ust=max(1.e-4,ust)
    corr=z/ust
    sigu=1.e-2+2.0*ust*exp(-3.e-4*corr)
    sigw=1.3*ust*exp(-2.e-4*corr)
    dsigwdz=-2.e-4*sigw
    sigw=sigw+1.e-2
    sigv=sigw
    tlu=0.5*z/sigw/(1.+1.5e-3*corr)
    tlv=tlu
    tlw=tlu


  !***********************
  ! 2. Unstable conditions
  !***********************

  else if (ol.lt.0.) then


  ! Determine sigmas
  !*****************

    sigu=1.e-2+ust*(12.-0.5*h/ol)**0.33333
    sigv=sigu
    sigw=sqrt(1.2*wst**2*(1.-.9*zeta)*zeta**0.66666+ &
         (1.8-1.4*zeta)*ust**2)+1.e-2
    dsigwdz=0.5/sigw/h*(-1.4*ust**2+wst**2* &
         (0.8*max(zeta,1.e-3)**(-.33333)-1.8*zeta**0.66666))


  ! Determine average Lagrangian time scale
  !****************************************

    tlu=0.15*h/sigu
    tlv=tlu
    if (z.lt.abs(ol)) then
      tlw=0.1*z/(sigw*(0.55-0.38*abs(z/ol)))
    else if (zeta.lt.0.1) then
      tlw=0.59*z/sigw
    else
      tlw=0.15*h/sigw*(1.-exp(-5*zeta))
    endif


  !*********************
  ! 3. Stable conditions
  !*********************

  else
    sigu=1.e-2+2.*ust*(1.-zeta)
    sigv=1.e-2+1.3*ust*(1.-zeta)
    sigw=sigv
    dsigwdz=-1.3*ust/h
    tlu=0.15*h/sigu*(sqrt(zeta))
    tlv=0.467*tlu
    tlw=0.1*h/sigw*zeta**0.8
  endif


  tlu=max(10.,tlu)
  tlv=max(10.,tlv)
  tlw=max(30.,tlw)

  if (dsigwdz.eq.0.) dsigwdz=1.e-10
end subroutine hanna

subroutine hanna1(z)
  !                  i
  !*****************************************************************************
  !                                                                            *
  !   Computation of \sigma_i and \tau_L based on the scheme of Hanna (1982)   *
  !                                                                            *
  !   Author: A. Stohl                                                         *
  !                                                                            *
  !   4 December 1997                                                          *
  !                                                                            *
  !*****************************************************************************
  !                                                                            *
  ! Variables:                                                                 *
  ! dsigwdz [1/s]     vertical gradient of sigw                                *
  ! ol [m]            Obukhov length                                           *
  ! sigu, sigv, sigw  standard deviations of turbulent velocity fluctuations   *
  ! tlu [s]           Lagrangian time scale for the along wind component.      *
  ! tlv [s]           Lagrangian time scale for the cross wind component.      *
  ! tlw [s]           Lagrangian time scale for the vertical wind component.   *
  ! ust, ustar [m/s]  friction velocity                                        *
  ! wst, wstar [m/s]  convective velocity scale                                *
  !                                                                            *
  !*****************************************************************************

  implicit none

  real :: z,s1,s2



  !**********************
  ! 1. Neutral conditions
  !**********************

  if (h/abs(ol).lt.1.) then

    ust=max(1.e-4,ust)
    sigu=2.0*ust*exp(-3.e-4*z/ust)
    sigu=max(sigu,1.e-5)
    sigv=1.3*ust*exp(-2.e-4*z/ust)
    sigv=max(sigv,1.e-5)
    sigw=sigv
    dsigw2dz=-6.76e-4*ust*exp(-4.e-4*z/ust)
    tlu=0.5*z/sigw/(1.+1.5e-3*z/ust)
    tlv=tlu
    tlw=tlu


  !***********************
  ! 2. Unstable conditions
  !***********************

  else if (ol.lt.0.) then


  ! Determine sigmas
  !*****************

    sigu=ust*(12.-0.5*h/ol)**0.33333
    sigu=max(sigu,1.e-6)
    sigv=sigu

    if (zeta.lt.0.03) then
      sigw=0.96*wst*(3*zeta-ol/h)**0.33333
      dsigw2dz=1.8432*wst*wst/h*(3*zeta-ol/h)**(-0.33333)
    else if (zeta.lt.0.4) then
      s1=0.96*(3*zeta-ol/h)**0.33333
      s2=0.763*zeta**0.175
      if (s1.lt.s2) then
        sigw=wst*s1
        dsigw2dz=1.8432*wst*wst/h*(3*zeta-ol/h)**(-0.33333)
      else
        sigw=wst*s2
        dsigw2dz=0.203759*wst*wst/h*zeta**(-0.65)
      endif
    else if (zeta.lt.0.96) then
      sigw=0.722*wst*(1-zeta)**0.207
      dsigw2dz=-.215812*wst*wst/h*(1-zeta)**(-0.586)
    else if (zeta.lt.1.00) then
      sigw=0.37*wst
      dsigw2dz=0.
    endif
    sigw=max(sigw,1.e-6)


  ! Determine average Lagrangian time scale
  !****************************************

    tlu=0.15*h/sigu
    tlv=tlu
    if (z.lt.abs(ol)) then
      tlw=0.1*z/(sigw*(0.55-0.38*abs(z/ol)))
    else if (zeta.lt.0.1) then
      tlw=0.59*z/sigw
    else
      tlw=0.15*h/sigw*(1.-exp(-5*zeta))
    endif


  !*********************
  ! 3. Stable conditions
  !*********************

  else
    sigu=2.*ust*(1.-zeta)
    sigv=1.3*ust*(1.-zeta)
    sigu=max(sigu,1.e-6)
    sigv=max(sigv,1.e-6)
    sigw=sigv
    dsigw2dz=3.38*ust*ust*(zeta-1.)/h
    tlu=0.15*h/sigu*(sqrt(zeta))
    tlv=0.467*tlu
    tlw=0.1*h/sigw*zeta**0.8
  endif




  tlu=max(10.,tlu)
  tlv=max(10.,tlv)
  tlw=max(30.,tlw)
end subroutine hanna1

subroutine hanna_short(z)
  !                       i
  !*****************************************************************************
  !                                                                            *
  !   Computation of \sigma_i and \tau_L based on the scheme of Hanna (1982)   *
  !                                                                            *
  !   Author: A. Stohl                                                         *
  !                                                                            *
  !   4 December 1997                                                          *
  !                                                                            *
  !*****************************************************************************
  !                                                                            *
  ! Variables:                                                                 *
  ! dsigwdz [1/s]     vertical gradient of sigw                                *
  ! ol [m]            Obukhov length                                           *
  ! sigu, sigv, sigw  standard deviations of turbulent velocity fluctuations   *
  ! tlu [s]           Lagrangian time scale for the along wind component.      *
  ! tlv [s]           Lagrangian time scale for the cross wind component.      *
  ! tlw [s]           Lagrangian time scale for the vertical wind component.   *
  ! ust, ustar [m/s]  friction velocity                                        *
  ! wst, wstar [m/s]  convective velocity scale                                *
  !                                                                            *
  !*****************************************************************************

  implicit none

  real :: z



  !**********************
  ! 1. Neutral conditions
  !**********************

  if (h/abs(ol).lt.1.) then
    ust=max(1.e-4,ust)
    sigw=1.3*exp(-2.e-4*z/ust)
    dsigwdz=-2.e-4*sigw
    sigw=sigw*ust+1.e-2
    tlw=0.5*z/sigw/(1.+1.5e-3*z/ust)


  !***********************
  ! 2. Unstable conditions
  !***********************

  else if (ol.lt.0.) then


  ! Determine sigmas
  !*****************

    sigw=sqrt(1.2*wst**2*(1.-.9*zeta)*zeta**0.66666+ &
         (1.8-1.4*zeta)*ust**2)+1.e-2
    dsigwdz=0.5/sigw/h*(-1.4*ust**2+wst**2* &
         (0.8*max(zeta,1.e-3)**(-.33333)-1.8*zeta**0.66666))


  ! Determine average Lagrangian time scale
  !****************************************

    if (z.lt.abs(ol)) then
      tlw=0.1*z/(sigw*(0.55-0.38*abs(z/ol)))
    else if (zeta.lt.0.1) then
      tlw=0.59*z/sigw
    else
      tlw=0.15*h/sigw*(1.-exp(-5*zeta))
    endif


  !*********************
  ! 3. Stable conditions
  !*********************

  else
    sigw=1.e-2+1.3*ust*(1.-zeta)
    dsigwdz=-1.3*ust/h
    tlw=0.1*h/sigw*zeta**0.8
  endif


  tlu=max(10.,tlu)
  tlv=max(10.,tlv)
  tlw=max(30.,tlw)
  if (dsigwdz.eq.0.) dsigwdz=1.e-10
end subroutine hanna_short

subroutine windalign(u,v,ffap,ffcp,ux,vy)
  !                     i i  i    i   o  o
  !*****************************************************************************
  !                                                                            *
  !  Transformation from along- and cross-wind components to u and v           *
  !  components.                                                               *
  !                                                                            *
  !     Author: A. Stohl                                                       *
  !                                                                            *
  !     3 June 1996                                                            *
  !                                                                            *
  !*****************************************************************************
  !                                                                            *
  ! Variables:                                                                 *
  ! ffap  turbulent wind in along wind direction                               *
  ! ffcp  turbulent wind in cross wind direction                               *
  ! u     main wind component in x direction                                   *
  ! ux    turbulent wind in x direction                                        *
  ! v     main wind component in y direction                                   *
  ! vy    turbulent wind in y direction                                        *
  !                                                                            *
  !*****************************************************************************

  implicit none

  real :: u,v,ffap,ffcp,ux,vy,ffinv,ux1,ux2,vy1,vy2,sinphi,cosphi
  real,parameter :: eps=1.e-30


  ! Transform along wind components
  !********************************

  ffinv=1./max(sqrt(u*u+v*v),eps)
  sinphi=v*ffinv
  vy1=sinphi*ffap
  cosphi=u*ffinv
  ux1=cosphi*ffap


  ! Transform cross wind components
  !********************************

  ux2=-sinphi*ffcp
  vy2=cosphi*ffcp


  ! Add contributions from along and cross wind components
  !*******************************************************

  ux=ux1+ux2
  vy=vy1+vy2
end subroutine windalign
function psih (z,l)

  !*****************************************************************************
  !                                                                            *
  !     Calculation of the stability correction term                           *
  !                                                                            *
  !     AUTHOR: Matthias Langer, adapted by Andreas Stohl (6 August 1993)      *
  !             Update: G. Wotawa, 11 October 1994                             *
  !                                                                            *
  !     Literature:                                                            *
  !     [1] C.A.Paulson (1970), A Mathematical Representation of Wind Speed    *
  !           and Temperature Profiles in the Unstable Atmospheric Surface     *
  !           Layer. J.Appl.Met.,Vol.9.(1970), pp.857-861.                     *
  !                                                                            *
  !     [2] A.C.M. Beljaars, A.A.M. Holtslag (1991), Flux Parameterization over*
  !           Land Surfaces for Atmospheric Models. J.Appl.Met. Vol. 30,pp 327-*
  !           341                                                              *
  !                                                                            *
  !     Variables:                                                             *
  !     L     = Monin-Obukhov-length [m]                                       *
  !     z     = height [m]                                                     *
  !     zeta  = auxiliary variable                                             *
  !                                                                            *
  !     Constants:                                                             *
  !     eps   = 1.2E-38, SUN-underflow: to avoid division by zero errors       *
  !                                                                            *
  !*****************************************************************************

  implicit none

  real :: psih,x,z,zeta,l
  real,parameter :: a=1.,b=0.667,c=5.,d=0.35,eps=1.e-20

  if ((l.ge.0).and.(l.lt.eps)) then
    l=eps
  else if ((l.lt.0).and.(l.gt.(-1.*eps))) then
    l=-1.*eps
  endif

  if ((log10(z)-log10(abs(l))).lt.log10(eps)) then
    psih=0.
  else
    zeta=z/l
    if (zeta.gt.0.) then
      psih = - (1.+0.667*a*zeta)**(1.5) - b*(zeta-c/d)*exp(-d*zeta) &
           - b*c/d + 1.
    else
      x=(1.-16.*zeta)**(.25)
      psih=2.*log((1.+x*x)/2.)
    end if
  end if

end function psih

real function psim(z,al)

  !**********************************************************************
  !                                                                     *
  ! DESCRIPTION: CALCULATION OF THE STABILITY CORRECTION FUNCTION FOR   *
  !              MOMENTUM AS FUNCTION OF HEIGHT Z AND OBUKHOV SCALE     *
  !              HEIGHT L                                               *
  !                                                                     *
  !**********************************************************************

  implicit none

  real :: z,al,zeta,x,a1,a2

  zeta=z/al
  if(zeta.le.0.) then
  ! UNSTABLE CASE
    x=(1.-15.*zeta)**0.25
    a1=((1.+x)/2.)**2
    a2=(1.+x**2)/2.
    psim=log(a1*a2)-2.*atan(x)+pi/2.
  else
  ! STABLE CASE
    psim=-4.7*zeta
  endif

end function psim

subroutine pbl_profile(ps,td2m,zml1,t2m,tml1,u10m,uml1,stress,hf)

  !********************************************************************
  !                                                                   *
  !                    G. WOTAWA, 1995-07-07                          *
  !                                                                   *
  !********************************************************************
  !                                                                   *
  ! DESCRIPTION: CALCULATION OF FRICTION VELOCITY AND SURFACE SENS-   *
  !              IBLE HEAT FLUX USING THE PROFILE METHOD (BERKOVICZ   *
  !              AND PRAHM, 1982)                                     *
  !                                                                   *
  ! Output now is surface stress instead of ustar                     *
  !                                                                   *
  !                                                                   *
  !********************************************************************
  !                                                                   *
  ! INPUT:                                                            *
  !                                                                   *
  !                                                                   *
  ! ps      surface pressure(Pa)                                      *
  ! td2m    two metre dew point(K)                                    *
  ! zml1    heigth of first model level (m)                           *
  ! t2m     two metre temperature (K)                                 *
  ! tml1    temperature first model level (K)                         *
  ! u10m    ten metre wind speed (ms-1)                               *
  ! uml1    wind speed first model level (ms-1)                       *
  !                                                                   *
  !********************************************************************
  !                                                                   *
  ! OUTPUT:                                                           *
  !                                                                   *
  ! stress  surface stress (i.e., friction velocity (ms-1) squared    *
  !                         multiplied with air density)              *
  ! hf      surface sensible heat flux (Wm-2)                         *
  !                                                                   *
  !********************************************************************
  ! ustar   friction velocity (ms-1)                                  *
  ! maxiter maximum number of iterations                              *
  !********************************************************************

  use qvsat_mod

  implicit none

  integer :: iter
  real :: ps,td2m,rhoa,zml1,t2m,tml1,u10m,uml1,ustar,hf
  real :: al,alold,aldiff,tmean,crit
  real :: deltau,deltat,thetastar,e,tv,stress
  integer,parameter :: maxiter=10
  real,parameter    :: r1=0.74

  e=ew(td2m,ps)               ! vapor pressure
  tv=t2m*(1.+0.378*e/ps)   ! virtual temperature
  rhoa=ps/(r_air*tv)       ! air density

  deltau=uml1-u10m         !! Wind Speed difference between
                           !! Model level 1 and 10 m

  if(deltau.le.0.001) then    !! Monin-Obukhov Theory not
    al=9999.               !! applicable --> Set dummy values
    ustar=0.01
    stress=ustar*ustar*rhoa
    hf=0.0
    return
  endif
  deltat=tml1-t2m+0.0098*(zml1-2.)  !! Potential temperature difference
                                    !! between model level 1 and 10 m

  if(abs(deltat).le.0.03) then    !! Neutral conditions
    hf=0.0
    al=9999.
    ustar=(vonkarman*deltau)/ &
         (log(zml1/10.)-psim(zml1,al)+psim(10.,al))
    stress=ustar*ustar*rhoa
    return
  endif

  tmean=0.5*(t2m+tml1)
  crit=(0.0219*tmean*(zml1-2.0)*deltau**2)/ &
       (deltat*(zml1-10.0)**2)
  if((deltat.gt.0).and.(crit.le.1.)) then
                                    !! Successive approximation will
    al=50.                          !! not converge
    ustar=(vonkarman*deltau)/ &
         (log(zml1/10.)-psim(zml1,al)+psim(10.,al))
    thetastar=(vonkarman*deltat/r1)/ &
         (log(zml1/2.)-psih(zml1,al)+psih(2.,al))
    hf=rhoa*cpa*ustar*thetastar
    stress=ustar*ustar*rhoa
    return
  endif

  al=9999.                 ! Start iteration assuming neutral conditions
  do iter=1,maxiter
    alold=al
    ustar=(vonkarman*deltau)/ &
         (log(zml1/10.)-psim(zml1,al)+psim(10.,al))
    thetastar=(vonkarman*deltat/r1)/ &
         (log(zml1/2.)-psih(zml1,al)+psih(2.,al))
    al=(tmean*ustar**2)/(ga*vonkarman*thetastar)
    aldiff=abs((al-alold)/alold)
    if(aldiff.lt.0.01) exit  !! Successive approximation successful
  end do
  hf=rhoa*cpa*ustar*thetastar
  if(al.gt.9999.) al=9999.
  if(al.lt.-9999.) al=-9999.

  stress=ustar*ustar*rhoa
end subroutine pbl_profile

end module turbulence_mod
